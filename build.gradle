plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "maven-publish"
}

group = "com.vicious"
version = "${mcver}-${projectver}"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "ViciousLib"
        url = uri("https://maven.pkg.${viciousorglink}/ViciousLib")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
        }
    }
}

dependencies {
    implementation "com.vicious:viciouslib:${vlver}"
    compileOnly "org.spigotmc:spigot-api:${mcver}-R0.1-SNAPSHOT"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
publishing {
    repositories {
        maven {
            name = "ViciousLibKit"
            url = uri("https://maven.pkg.github.com/Vicious-MCModding/ViciousLibKit")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "${group}"
            artifactId = 'viciouslibkit'
            version = version
            from components.java
        }
    }
}
